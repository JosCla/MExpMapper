.General Utilities

Lbl CPRV {
    .Just the Copy() command but copies bytes in reverse order
    .(Good for copying a portion of memory to an overlapping portion
    .  that's forwards in memory)
    .r1: Base memory address (source)
    .r2: Base memory address (target)
    .r3: Number of bytes to copy
    .(returns nothing)

    For(G,1,r_3)
        {r_1+r_3-G}->{r_2+r_3-G}
    End
}

Lbl DGWP {
    .DispGraph with parity
    .(Will call DispGraph while also tracking its parity)

    DispGraph^r
    1-CP->CP

    Return
}

Lbl DGUP {
    .DispGraph until a certain parity is met
    .r1: Desired parity

    If CP!=r_1
        DGWP()
    End

    Return
}

Lbl MENU {
    .Displays a menu
    .r1: Number of menu choices
    .r2: Array of pointers to strings to display as options
    .r3-r4: posX, posY of upper-left corner of choices
    .Returns the number corresponding to the user's choice

    .Displaying choices
    For(G,1,r_1)
        G-1*8+r_4->H
        Text(r_3,H,G>Dec)
        Text(r_3+6,H,{G-1*2+r_2}^r)
    End

    .Awaiting user response
    While 1
        DGWP()
        For(G,1,r_1)
            {G+GDB1NCOD}->H
            If getKey(H)
                GReturn
            End
        End
    End

    .(should never reach this point)
    Return
}
