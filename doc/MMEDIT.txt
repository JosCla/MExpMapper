.Map EDIT utilities

Lbl CHHT {
    .Change terrain height
    .r1-r2: PosX, PosY
    .r3: Change in height

    {r_2*MW+r_1+2+HM}+r_3->r_3
    STHT()

    Return
}

Lbl STHT {
    .Set terrain height
    .r1-r2: PosX, PosY
    .r3: Target terrain height

    r_3->{r_2*MW+r_1+2+HM}

    Return
}

Lbl TGWT {
    .Toggle water at
    .r1-r2: PosX, PosY

    r_2*MW+r_1+2+OM->G
    {G} xor 2->{G}

    Return
}

Lbl TGST {
    .Toggle stump at
    .r1-r2: PosX, PosY

    .Getting pointer to properties byte
    r_2*MW+r_1+2+OM->G

    .Increasing/decreasing terrain height as needed
    {G}e5?
        -1->r_3,
        1->r_3
    Select(G,
        CHHT()
    )->G

    .Toggling stump bit
    {G} xor 4->{G}

    Return
}

Lbl TGRK {
    .Toggle rock at
    .r1-r2: PosX, PosY

    r_2*MW+r_1+2+OM->G
    {G} xor 8->{G}

    Return
}

Lbl TGLD {
    .Toggle ladder at
    .r1-r2: PosX, PosY

    r_2*MW+r_1+2+OM->G
    {G} xor 1->{G}

    Return
}

Lbl TGGT {
    .Toggle ground texture at
    .r1-r2: PosX, PosY

    r_2*MW+r_1+2+OM->G
    nib{G*2}+1->H
    H>=4?0->H
    H->nib{G*2}

    Return
}

Lbl SPBA {
    .Set property byte at
    .r1-r2: PosX, PosY
    .r3: Property byte

    r_3->{r_2*MW+r_1+2+OM}

    Return
}
